/**
 * Yarel
 * Copyright (C) 2018  Claudio Grandi, Dariush Moshiri, Luca Roversi
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */


grammar org.di.unito.yarel.Yarel with org.eclipse.xtext.common.Terminals
// needed because of the usage of Ecore type EBoolean
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate yarel "http://www.di.org/unito/yarel/Yarel"

Model:
    imports += Import* 'module' name=ID '{' elements+=Element* '}'; //Changed by Matteo Palazzo
	
Element: 
	  Declaration | Definition;

Import:
	'import' importedNamespace=QualifiedNameWithWildcard; //Changed by Matteo Palazzo

QualifiedName:
   ID ('.' ID)*;

QualifiedNameWithWildcard:
    ID ('.' ID)* '.' (ID | '*');  //Changed by Matteo Palazzo

/* FUNCTION DECLARATION & DEFINITION */

Declaration:
	'dcl' name=ID 
	':' (types += Type (',' types+=Type)*)
;

Type:
    {Type} value=(INT)? 'int';


Definition:
	'def' declarationName=[Declaration] ':=' body=Body;



/* BODY FUNCTION */

Body: SerComp;

SerComp returns Body:
	ParComp ({SerComp.left=current} ';' right=ParComp)*;

ParComp returns Body:
	BodyBase ({ParComp.left=current} '|' right=BodyBase)* ;

BodyBase returns Body: 
   '(' Body ')' | Atomic;

Permutation: 
  '/' indexes+=Digit (indexes+=Digit)* '/';
 

FunctionInvocation:
	funName=[Declaration | QualifiedName]
;

/**
 * Indexes are 1-based.
 */
Atomic returns Body: 
    {BodyId}  funName ='id' 
  | {BodyInc} funName ='inc' 
  | {BodyDec} funName ='dec'
  | {BodyNeg} funName ='neg'
  | {BodyFor} function='for' '[' body=Body ']'
  | {BodyInv} function='inv' '[' body=Body ']'
  | {BodyIt}  function='it' '[' body=Body ']'
  | {BodyIf}  function='if' '[' pos=Body ',' zero=Body ',' neg=Body ']'
  | {BodyFun} function=FunctionInvocation
  | {BodyPerm} permutation=Permutation
;

Digit:
	value=INT;